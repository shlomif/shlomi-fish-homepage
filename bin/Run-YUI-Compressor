#!/usr/bin/perl

use strict;
use warnings;

use Getopt::Long qw/ GetOptions /;
use Path::Tiny qw/ path /;
use File::Temp ();

my $output_fn;

GetOptions( '-o=s' => \$output_fn, );

if ( !defined $output_fn )
{
    die "Output filename not specified";
}

my $buf;

foreach my $fn (@ARGV)
{
    $buf .= ( path($fn)->slurp_utf8 =~ s/([^\n])\z/$1\n/mrs );
}

my $t_dir = File::Temp->newdir;

my $temp_fn = $t_dir->dirname() . '/temp.js';

path($temp_fn)->spew_utf8($buf);

if ( system( 'yuicompressor', '-o', $output_fn, $temp_fn ) )
{
    if ( $ENV{KEEP_COPY} )
    {
        path($temp_fn)->copy( $ENV{KEEP_COPY} );
    }
    die "yuicompressor returned an error - $!";
}
