#!/usr/bin/perl

use strict;
use warnings;
use autodie;

use Getopt::Long qw/ GetOptions /;
use File::Which qw/ which /;
use YAML::XS qw/ LoadFile /;
use Path::Tiny qw/ path /;

my $out_fn;
my $modules_fn = 'bin/required-modules.yml';

GetOptions(
    'o|output=s'     => \$out_fn,
) or die "Failed - $!";

open my $o, '>', $out_fn;
$o->print(<<"EOF");
Summary:	Require shlomif homesite deps
Name:		task-shlomif-homesite
Version:	0.0.1
Release:	%mkrel 1
License:	MIT
Group:		System
Url:		http://www.shlomifish.org/
BuildArch:	noarch
EOF
{
    my ($yaml_data) = LoadFile($modules_fn);

    {
        foreach my $line ( @{ $yaml_data->{required}->{executables} } )
        {
            my $cmd = $line->{exe};
            if ($cmd eq 'sass')
            {
                $cmd = 'ruby-sass';
            }
            elsif ($cmd eq 'convert')
            {
                $cmd = 'imagemagick';
            }
            elsif ($cmd eq 'node')
            {
                $cmd = 'nodejs';
            }
            $o->print("Requires: $cmd\n");
        }
    }
    {
        my $required_modules = $yaml_data->{required}->{perl5_modules};

        foreach my $m ( sort { $a cmp $b } keys(%$required_modules) )
        {
            $o->print("Requires: perl($m)\n");
        }
    }
    {
        my @required_modules = keys %{ $yaml_data->{required}->{py3_modules} };

        foreach my $module (@required_modules)
        {
            if ($module eq 'bs4')
            {
                $module = 'beautifulsoup4';
            }
            $o->print("Requires: python3dist($module)\n");
        }
    }
}

$o->print(<<"EOF");

%description
This task package installs some of the packages that Shlomi Fish needs.

%files

%changelog
* Mon Jan 12 2015 shlomif <shlomif\@shlomifish.org> 0.2.0-1.mga5
- Initial package.
EOF
close($o);
exit(0);
